plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.google.devtools.ksp'

}

android {
    def target = 34
    buildToolsVersion = "34.0"
    namespace 'com.token.tokenator'
    compileSdk target

    defaultConfig {
        applicationId "com.token.tokenator"
        minSdkVersion 23
        targetSdkVersion target
        versionCode 46
        versionName "1.64"

        testInstrumentationRunner "com.token.tokenator.HiltAndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kapt {
        correctErrorTypes true
    }

    kotlinOptions {
        jvmTarget = '17'
    }
    lint {
        disable 'JvmStaticProvidesInObjectDetector', 'FieldSiteTargetOnQualifierAnnotation', 'ModuleCompanionObjects', 'ModuleCompanionObjectsNotInModuleParent'
    }

    buildTypes {
        release {
            buildConfigField("String", "SECRET_KEY", "\"${System.getenv('SECRET_KEY') ?: getProperty("SECRET_KEY")} \"")
            buildConfigField("String", "SALT", "\"${System.getenv('SALT') ?: getProperty("SALT")}\"")
            buildConfigField("String", "IV", "\"${System.getenv('IV') ?: getProperty("IV")}\"")
        }

        debug {
            buildConfigField("String", "SECRET_KEY", "\"${System.getenv('SECRET_KEY') ?: getProperty("SECRET_KEY")} \"")
            buildConfigField("String", "SALT", "\"${System.getenv('SALT') ?: getProperty("SALT")}\"")
            buildConfigField("String", "IV", "\"${System.getenv('IV')  ?: getProperty("IV")}\"")
        }
    }

    android.packagingOptions {
        exclude 'META-INF/*'
    }
}

ktlint {
    android = true
    ignoreFailures = false
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "sarif"
    }
}

tasks.getByPath("preBuild").dependsOn("ktlintFormat")

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.0"

    // Room Database
    implementation "androidx.room:room-runtime:2.6.1"
    annotationProcessor "androidx.room:room-compiler:2.6.1"

    implementation "androidx.room:room-ktx:2.6.1"

    // To use Kotlin Symbol Processing (KSP)
    ksp "androidx.room:room-compiler:2.6.1"

    // optional - Test helpers
    testImplementation "androidx.room:room-testing:2.6.1"

    // optional - Paging 3 Integration
    implementation "androidx.room:room-paging:2.6.1"

    // Navigation
    def nav_version = "2.7.7"

    implementation "androidx.navigation:navigation-fragment-ktx:2.7.7"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.7"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:2.7.7"


    // DI
    implementation "com.google.dagger:hilt-android:2.51.1"
    ksp "com.google.dagger:hilt-android-compiler:2.51.1"
    ksp "androidx.hilt:hilt-compiler:1.2.0"

    // Firebase
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:33.0.0')

    // Add the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Add the dependency for the Realtime Database library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-database-ktx'

    // Add the dependency for the Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Add Performance lib
    implementation 'com.google.firebase:firebase-perf-ktx'

    // For instrumented tests.
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.51.1'
    // ...with Kotlin.
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.51.1'

    // Styling
    implementation "com.google.android.material:material:1.12.0"

    // DataBinding & other
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.0'

    // Fluid Slider
    implementation 'com.ramotion.fluidslider:fluid-slider:0.3.1'

    // Feature discovery
    implementation 'uk.co.samuelwall:material-tap-target-prompt:3.3.2'

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.1.1"

    // TESTING
    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:2.7.7"

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.2'

    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:5.4.0'

    // Core library
    androidTestImplementation 'androidx.test:core:1.5.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'com.google.android.apps.common.testing.accessibility.framework:accessibility-test-framework:4.1.1'
    androidTestImplementation 'com.github.JoshLudahl:espresso-library-kotlin:2023.9.01'

    androidTestUtil 'androidx.test:orchestrator:1.4.2'
}
